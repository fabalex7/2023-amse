pipeline StopsPipeline {

    valuetype Coordinates oftype decimal {
        constraints: [
            Between90,
        ];
    }
    
    valuetype ZoneNumber oftype integer {
        constraints: [
            Zone2001,
        ];
    }

    constraint Between90 oftype RangeConstraint {
        lowerBound: -90;
        upperBound: 90;
    }

    constraint Zone2001 oftype RangeConstraint {
        lowerBound: 2001;
        upperBound: 2001;
    }
    
    block StopsExtractor oftype HttpExtractor {
        url: "https://gtfs.rhoenenergie-bus.de/GTFS.zip";
    }

    block StopsArchiveInterpreter oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block StopsFilePicker oftype FilePicker {
        path: "/stops.txt";
    }

    block StopsTextFileInterpreter oftype TextFileInterpreter {

    }

    block StopsCSVInterpreter oftype CSVInterpreter {
        delimiter: ",";
        enclosing: '"';
    }

    block StopsTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "stop_id" oftype integer,
            "stop_name" oftype text,
            "stop_lat" oftype Coordinates,
            "stop_lon" oftype Coordinates,
            "zone_id" oftype ZoneNumber
        ];
    }

    block StopsLoader oftype SQLiteLoader {
        table: "stops";
        file: "./gtfs.sqlite";
    }

    StopsExtractor
        ->StopsArchiveInterpreter
        ->StopsFilePicker
        ->StopsTextFileInterpreter
        ->StopsCSVInterpreter
        ->StopsTableInterpreter
        ->StopsLoader;
}